apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]

android {
    namespace 'is.xyz.mpv'
    compileSdk 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 31
        versionName "2023-06-04-release"
        vectorDrawables.useSupportLibrary = true
    }

    // We already have the libs compiled, just point to them. (because I'm a Windows user)
    sourceSets.main.jniLibs.srcDirs = ['src/main/libs']

    // Debug name so we don't interfere with official app
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions.add("default")
    productFlavors {
        "default" {
            isDefault true
        }
        api29 {
            targetSdkVersion 29
            versionNameSuffix "-oldapi"
        }
    }

    buildFeatures {
        viewBinding = true
        compose = true
    }

    // https://developer.android.com/jetpack/androidx/releases/compose#declaring_dependencies
    composeOptions.kotlinCompilerExtensionVersion = "1.4.7"

    splits {
        abi {
            enable true
            reset()
            // include only the ABIs that were actually built
            project.ext.abiCodes.each { abi, _ ->
                if (new File(project.projectDir, "src/main/jniLibs/${abi}").exists())
                    include(abi)
            }
            universalApk true // build an APK with all ABIs too
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

// Map versionCode so each ABI gets a different one
// e.g. x86 with version 21 gets a versionCode of 3021
import com.android.build.VariantOutput

android.applicationVariants.configureEach { variant ->
    variant.outputs.each { output ->
        def base = project.ext.abiCodes.get(output.getFilter(VariantOutput.ABI))
        // universal APK keeps the original versionCode
        if (base != null)
            output.versionCodeOverride = base * 1000 + variant.versionCode
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // https://developer.android.com/jetpack/androidx/releases/compose
    def composeBom = platform("androidx.compose:compose-bom:2023.05.01")
    implementation(composeBom)
    debugImplementation("androidx.compose.ui:ui-tooling")
    implementation("androidx.compose.animation:animation")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-core")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha10")

    // https://mvnrepository.com/artifact/com.google.accompanist/accompanist-systemuicontroller
    def accompanist = "0.31.3-beta"
    implementation("com.google.accompanist:accompanist-systemuicontroller:$accompanist")
    implementation("com.google.accompanist:accompanist-permissions:$accompanist")

    // https://mvnrepository.com/artifact/com.github.alorma/compose-settings-ui-m3
    implementation("com.github.alorma:compose-settings-ui-m3:0.27.0")

    implementation("androidx.activity:activity-compose:1.7.2")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.6.1")
    implementation("androidx.media:media:1.6.0")
    implementation("androidx.preference:preference-ktx:1.2.0")

    // TODO: Remove once we're fully compose
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
}
